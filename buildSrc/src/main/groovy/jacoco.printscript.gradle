plugins {
    id 'jacoco'
}

jacoco {
    toolVersion = "0.8.12"
}

tasks.withType(Test).configureEach {
    finalizedBy jacocoTestReport // Report is always generated after tests run
}

// Generates the report of the code coverage
tasks.withType(JacocoReport).configureEach {
    dependsOn test // Tests are required to run before generating the report
    reports {
        xml.required = false
        csv.required = false
        html.required = true
        html {
            destination = file("${rootDir}/reports/jacoco/${project.name}")
        }
    }
    classDirectories.setFrom(
            files(classDirectories.files.collect {
                fileTree(dir: it, exclude: [
                        '**/cli/**',
                        '**/runner/**',
                        '**/parser/ast/**'
                ])
            })
    )
}

// Verifies the code coverage
tasks.withType(JacocoCoverageVerification).configureEach {
    violationRules {
        rule {
            limit {
                minimum = 0.8 // Establece el m√≠nimo aceptable de cobertura
            }
        }
    }
}

tasks.named('build') {
    dependsOn tasks.withType(JacocoCoverageVerification)
}

